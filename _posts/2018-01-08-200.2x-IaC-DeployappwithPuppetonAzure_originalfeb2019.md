


In this hands-on lab, you will deploy a Java app, the Parts Unlimited MRP App, using Puppet from <a href="https://puppetlabs.com/" target="_blank"><span style="color: #0066cc;" color="#0066cc">PuppetLabs</span></a>. Puppet is a configuration management system that allows you to automate provisioning and configuration of machines by describing the state of your infrastructure as code. Infrastructure as Code is an important pillar of good DevOps.


<h3><span style="color: #0000CD;">DevOps MPP Course Source </span></h3>

- This lab is used in course <a href="https://www.edx.org/course/infrastructure-code-microsoft-devops200-2x-0" target="_blank"><span style="color: #0066cc;" color="#0066cc">DevOps200.2x: Infrastructure as Code</span></a> - Module 4.

<h3><span style="color: #0000CD;"> Pre-requisites:</span></h3>

- An SSH client such as PuTTY
- An Azure subscription

<h3><span style="color: #0000CD;"> Lab Tasks:</span></h3>


- Provision a **Puppet Master** and **node** (both Ubuntu VMs) in Azure using ARM templates
- Install Puppet Agent on the node
- Configure the Puppet Production Environment
- Test the Production Environment Configuration
- Create a Puppet program to describe the environment for the MRP application
- Run the Puppet Configuration on the Node


<h3><span style="color: #0000CD;">Estimated Lab Time:</span></h3>

- approx. 80 minutes  


### Task 1: Provision a **Puppet Master** and **node** (both Ubuntu VMs) in Azure using ARM templates

In this lab you will work with two machines: 

- a **Puppet Master** machine 
- another machine known as a **node** which will host the MRP application.
    
The only task you will perform on the node is to install the Puppet agent. The rest of the configuration will be applied by instructing Puppet how to configure the node through _puppet programs_ on the Puppet Master. 

1. Provision the Lab machines using an Azure Resource Manager (ARM) Template

    This lab calls for the use of two machines. The Puppet Master server **must** be a Linux machine, but the puppet agent can run on Linux or Windows. For this lab, the _node_ that we will be configuring is an **Ubuntu VM**.

    Instead of manually creating the VMs in Azure, we are going to use an Azure Resource Management (ARM) template.
    
2. Click on the **Deploy to Azure** button
    
    Simply click the Deploy to Azure button below and follow the wizard to deploy the two machines. You will need
    to log in to the Azure Portal.
                                                                     
    <a href="https://portal.azure.com/#create/Microsoft.Template/uri/https%3A%2F%2Fraw.githubusercontent.com%2FMicrosoft%2FPartsUnlimitedMRP%2Fmaster%2FLabfiles%2FDevOps200.2x-InfrastructureasCode%2FPuppet%2Fenv%2FPuppetPartsUnlimitedMRP.json" target="_blank">
        <img src="http://azuredeploy.net/deploybutton.png"/>
    </a>

    <a href="http://armviz.io/#/?load=https%3A%2F%2Fraw.githubusercontent.com%2FMicrosoft%2FPartsUnlimitedMRP%2Fmaster%2FLabfiles%2FDevOps200.2x-InfrastructureasCode%2FPuppet%2Fenv%2FPuppetPartsUnlimitedMRP.json" target="_blank">
        <img src="http://armviz.io/visualizebutton.png"/>
    </a>

    The VMs will be deployed to a Resource Group along with a virtual network (VNET) and some other required resources. You can 
    delete the resource group in order to remove all the created resources at any time.

    If you have time you can review the template json file and script that are been run. You can do so in the <a href="https://github.com/Microsoft/PartsUnlimitedMRP/tree/master/Labfiles/DevOps200.2x-InfrastructureasCode/Puppet" target="_blank"><span style="color: #0066cc;" color="#0066cc">Puppet Labfiles folder</span></a> 



3. Specify settings for the deployment
    
    You will need to select a subscription and region to deploy the Resource Group to and to supply an admin username and password and unique public dns names for both machines. Some pointers to make it as easy as posisble are 

    - use the same user name for the admin usernames i.e. **azureuser**
    - use the same password for each password i.e. **Passw0rd0134**
    - include the word **master** in the puppet master VM name to easily distinguish it from the node VM, where you will deploy your app to. You could include the word **node** in the node VM name also if you wish i.e. partsmrpnode
    - add your initials and a number to the end of the dns public names to ensure they are unique i.e. **partspuppetmasterek01** and **partsmrpnodeek01**
    
        The *Puppet Master* will be a **Standard D2_V2** while the *partsmrp* machine
        will be a **Standard A2**.

    ![](..\assets\pumrpdeploywithpuppet-jan2018\shot1.png)
    ![](..\assets\pumrpdeploywithpuppet-jan2018\shot2.png)

    Make sure you make a note of the region as well as the usernames and passwords for the machines. Allow
    about 10 minutes for deployment and then another 10 minutes for the Puppet Master to configure Puppet. 

4. Check the Resource Group in the Azure Portal
    When the deployment completes, ensure it is fully deployed which you can view under **Overview** > **Deployments** > **1 succeeded**, and you can click on the **1 Succeeded** link to view the deployment details. You should see the following resources in the Azure Portal:

    Click on the **partspuppetmaster** Public IP Address. Then make a note of the DNS name:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\shot3.png)
    ![](..\assets\pumrpdeploywithpuppet-jan2018\shot4.png)

    The _dnsaddress_ will be of the form _machinename_._region_.cloudapp.azure.com. Open a browser to https://_dnsaddress_.
    (Make sure you're going to *http`s`*, and **not** *http*). You will be prompted about an invalid certificate - it is safe to
    ignore this for the purposes of this lab. If the Puppet configuration has succeeded, you should see the Puppet Console
    sign in page:

   ![](..\assets\pumrpdeploywithpuppet-jan2018\puppet_masterwebsitewarning.png)



    ![](..\assets\pumrpdeploywithpuppet-jan2018\4.jpg)

    >**Note:** The lab requires several ports to be open, such as the Puppet Server port, the Puppet console port, SSH ports and the Parts Unlimited MRP app port on the partsmrp machine. The ARM template opens these ports on the
    machines for you. You can look through the deployment json template to view the port configuration in the <a href="https://github.com/Microsoft/PartsUnlimitedMRP/blob/master/Labfiles/DevOps200.2x-InfrastructureasCode/Puppet/env/PuppetPartsUnlimitedMRP.json" target="_blank"><span style="color: #0066cc;" color="#0066cc">PuppetPartsUnlimitedMRP.json</span></a>


5. Log in to the Puppet Console

    Now go back to the Puppet Console in your browser and enter the username `admin` and the password you specified earlier for Pm Console Password i.e.
    
    - user name = `admin`
    - Password= **console password you specified earlier i.e. `Passw0rd0134`**

    
    **Note**: You will not be able to log into the puppet master console with the user name you specified earlier, the `admin` account is a built in account that you need to use here. 

    You should be able to successfuly log in now and when you log in, you should see a page like this:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\6.jpg)


### Task 2: Install Puppet Agent on the node

You are now ready to add the node to the Puppet Master. Once the node is added, the Puppet Master will be able to configure the node.

1. Get the Puppet Master internal DNS name
    Go to **Nodes** > **Unsigned Certificates**. The page that loads will show a command that we need to run on the node. In the example below, the puppet master machine name is:

    ```
    partspuppetmaster.nqkkrckzqwwu1p5pu4ntvzrona.cx.internal.cloudapp.net
    ```
    ![](..\assets\pumrpdeploywithpuppet-jan2018\8.jpg)

    and the command that is listed will be something like

    ```sh
    curl -k https://partspuppetmasterek01.irblmudbrloe5hz001blu2g34f.ax.internal.cloudapp.net:8140/packages/current/install.bash | sudo bash
    ```

2. Obtain the DNS name of the node or mrp virtual machine in Azure by opening up the Virtual machine properties and noting the DNS name

    ![](..\assets\pumrpdeploywithpuppet-jan2018\puppet_mrpvmdns.png)

3. Connect to the node VM using ssh or PuTTy.

    ![](..\assets\pumrpdeploywithpuppet-jan2018\puppet_sshtonodevm.png)

4. Run the "Add Node" command on the node
    
    Copy the "Add Node" command from the Puppet Console (the one that starts with `curl...`) and go to the SSH terminal of
    the node. Run the command.
    ![](..\assets\pumrpdeploywithpuppet-jan2018\11.jpg)

    ![](..\assets\pumrpdeploywithpuppet-jan2018\puppet_installpuppegentonnode.png)
 

    The command will install the puppet agent and any dependencies on the node. It take two to three minutes to complete.

    From here on, you will configure the node only from the Puppet Master, though you will use the partsmrp SSH terminal to manually force Puppet to configure it.

5. Accept the Pending Node Request
    
    Return to the **Puppet Console** and **refresh** the **Unsigned Certificates** page (where you previously got the node install command). You
    should see a pending request. This request has come from the node and will authorize the certificate between the puppet master and the node so that they can communicate securely. Press "Accept" to approve the node:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\12.jpg)

6. Click on the **Nodes** tab in the Puppet Console to return to the nodes view. You should see 2 nodes listed: 
	- the puppet master and the 
    - partsmrp node 
    
    (it may take a few minutes for the mrp node to finish configuration before it appears)

    If you go to **Configuration** > **Overview** in the Puppet Console you will also see the nodes listed.

    ![](..\assets\pumrpdeploywithpuppet-jan2018\13.jpg)

    >**Note:** It is possible to automate the install and configuration of the Puppet agent onto an Azure VM using the <a href="https://github.com/Azure/azure-quickstart-templates/tree/master/puppet-agent-windows" target="_blank"><span style="color: #0066cc;" color="#0066cc">Puppet Agent extension</span></a> from the Azure Marketplace.

    There are also a series of PowerShell cmdlets available for provisioning, enabling, and disabling the Puppet extension handler on Windows virtual machines (VMs). Essentially, this provides a command-line interface that allows users to deploy Puppet Enterprise agents to Windows VMs in Azure. Details of these are avaliable in the <a href="https://puppet.com/sites/default/files/Microsoft-Powershell-cmdlets.pdf" target="_blank"><span style="color: #0066cc;" color="#0066cc">Puppet PowerShell Cmdlets for Azure guide</span></a>



### Task 3: Configure the Puppet Production Environment

The Parts Unlimited MRP application is a Java application that requires [mongodb](https://www.mongodb.org/)
and [tomcat](http://tomcat.apache.org/) to be installed and configured on the partsmrp machine (the node). Instead of installing and configuring manually, we will now write a puppet program that will instruct the node how to configure
itself.

Puppet Programs are stored in a particular folder in the puppet master. Puppet programs are made up of manifests that describe the desired state of the node(s). The manifests can consume modules, which are pre-packaged Puppet Programs. Users can create their own modules or consume modules from a marketplace maintained by PuppetLabs known as the <a href="http://forge.puppetlabs.com" target="_blank"><span style="color: #0066cc;" color="#0066cc">Forge</span></a>. Some modules on the Forge are official modules that are supported - others are open-source modules uploaded from the community. You can search on this page for particular areas which may have modules available for them.

Puppet Programs are organized by environment, allowing you to manage different catalogs for different environments such as dev, test and production.

For the purposes of this lab, we will treat the node as if it were in the production environment. We will also need to download a few modules from the Forge which we will consume to configure the node.

When the Puppet Server was installed in Azure, it configured a folder for managing the production environment in `/etc/puppetlabs/code/environments/production`.

1. Inspect the Production modules
    Connect to the to the Puppet Master via an ssh terminal using ssh or PuTTy and then `cd` to the production folder:

    ```sh
    cd /etc/puppetlabs/code/environments/production
    ```
    
    ![](..\assets\pumrpdeploywithpuppet-jan2018\puppet_manifestmoduelfolders.png)


    If you run `ls` you will see the folders `manifests` and `modules` folders. 
    - The `manifests` folder contains descriptions of machines that we will later apply to nodes. 
    - The `modules` folder contains any modules that are referenced within the manifests.

2. Install additional **Puppet Forge Modules:** 

    We will now install some modules from the Puppet Forge that we will need to configure the `partsmrp` node. Run the following 4 commands in the open ssh terminal connection to the puppet master:

    ```sh
    sudo puppet module install puppetlabs-mongodb
    sudo puppet module install puppetlabs-tomcat
    sudo puppet module install maestrodev-wget
    sudo puppet module install puppetlabs-accounts
    sudo puppet module install puppetlabs-java
    ```

    ![](..\assets\pumrpdeploywithpuppet-jan2018\14.jpg)

    >**Note:** The `mongodb` and `tomcat` modules are supported modules from the Forge. The `wget` module is a user module and so is not officially supported. The accounts module will provide us with puppet classes to manage and create user and groups on the linux machine, and finally the Java module will provide with some functionality around Java.

3. Create a Custom Module

    Still on the pppet master, we will now create a custom module that will configure the Parts Unlimited MRP app. Run the following commands to template a module:

    ```sh
    cd /etc/puppetlabs/code/environments/production/modules
    ```
    
    followed by

    ```sh
    sudo puppet module generate partsunlimited-mrpapp
    ```

    This will start a wizard that will ask a series of questions as it scaffolds the module. Simply press `enter` for each question (accepting blank or default) until the wizard completes.

    Running `ls -la` should list the modules available so far, including `mrpapp`:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\15.jpg)

4. Configure the default node in the site.pp file
    
    We are going to define the node's configuration in the `mrpapp` module. The configuration of the nodes in the production environment is defined in a `site.pp` file in the production `manifests` folder (the `.pp` extension is short for "puppet program"). Let's edit the `site.pp` file and define the configuration for our node by running the following command:

    ```sh
    sudo nano /etc/puppetlabs/code/environments/production/manifests/site.pp
    ```

    Scroll to the bottom of the file and edit the `node default` section. Edit it to look as follows:

    ```puppet
    node default {
      class { 'mrpapp': }
    }
    ```

    **Note:** make sure you remove the comment tag **#** before the class definition, as in the screenshot below and text example above.

    ![](..\assets\pumrpdeploywithpuppet-jan2018\siteppclassconfig.png)
  

    Press `cntrl-X`, then `y` then `enter` to save the changes to the file.

    This instructs Puppet to configure the `default` (that is, all nodes) with the `mrpapp` module. The module (though currently empty) is in the `modules` folder of the production environment, so Puppet will know where to find it.

### Task 4: Test the Production Environment Configuration

Before we fully describe the MRP app for the node, let's test that everything is hooked up correctly by configuring a "dummy" file in the `mrpapp` module. If Puppet executes and creates the dummy file, we know everything is configured and working corrcetly up to this point and then we can flesh out the rest of the module properly.

1. Edit the init.pp file
    
    Let's edit the `init.pp` file of the `mrpapp` module on the puppet master (this is the entry point for the module):

    ```sh
    sudo nano /etc/puppetlabs/code/environments/production/modules/mrpapp/manifests/init.pp
    ```

    You can either delete all the boiler-plate comments or just ignore them. Scroll down to the `class mrpapp`     declaration and make it look as follows:

    ```puppet
    class mrpapp {
        file { '/tmp/dummy.txt':
            ensure => 'present',
            content => 'Puppet rules!',
        }
    }
    ```

    ![](..\assets\pumrpdeploywithpuppet-jan2018\mrpappmoduleconfig.png)

    Press `cntrl-X`, then `y` then `enter` to save the changes to the file.

    >**Note:** Classes in Puppet programs are not like classes in Object Oriented Programming. They simply define  a "resource" that is conifgured on a node. In the `mrpapp` class (or resource), we have just instructed Puppet to ensure that a file exists at the path `/tmp/dummy.txt` that has the content "Puppet rules!". We 
    will define more advanced resources within the `mrpapp` class as we progress.

2. Test the dummy file

    Let's test our setup. Switch to the node or mrp ssh terminal, i.e the `partsmrp` SSH terminal and enter the following command:

    ```sh
    sudo puppet agent --test --debug
    ```

    ![](..\assets\pumrpdeploywithpuppet-jan2018\testdummytestnodeconfig.png)

    We use the `--debug` switch to display more information as the command exectutes, purely for learning purposes. You can remove this switch to receive less text output if you wish.

    By default, the Puppet agents will query the Puppet Master for their configuration every 30 minutes. The
    command you just entered forces the agent to ask the Puppet Master for its configuration. It then tests
    itself against the configuration, and does whatever it needs to do in order to make itself match that
    configuration. In this case, the configuration requires the `/tmp/dummy.txt` file, so the node creates
    the file accordingly.

    You may see more configuration and output then is in the screenshot, as the screenshot is from a second run of the command but should see a successful run on the node. `cat` the `/tmp/dummy.txt` file to inspect its contents:

    ```sh
    cat /tmp/dummy.txt
    ```

    ![](..\assets\pumrpdeploywithpuppet-jan2018\dummyfilepoutput.png)

3. Correct configuration drift

    Puppet will automatically detect configuration drift and fix it. By default, the agent runs every 30 minutes on
    the nodes. Each time the agent runs, Puppet will determine if the environment is in the correct state - if it is
    not, it will reapply classes as necessary.

    Let's simulate configuration drift by deleting the dummy file and then re-running the puppet agent:

    ```sh
    sudo rm /tmp/dummy.txt
    sudo puppet agent --test
    cat /tmp/dummy.txt
    ```

    You should see the run complete successfully and the file should exist again. 

    ![](..\assets\pumrpdeploywithpuppet-jan2018\17.jpg)

    You can also try to edit the contents of the file and re-run the `sudo puppet agent --test` command to see the 
    contents update.

### Task 5: Create a Puppet Program to Describe the Prerequisites for the MRP Application

Now that we have hooked up the node (partsmrp) to the Puppet Master, we can begin to write the Puppet Program
that will describe the prerequisites for the Parts Unlimited MRP application.

>**Note:** For simplicity, we will describe the entire configuration in a single Puppet Program (init.pp from the mrpapp module we created earlier). However, the parts of the configuration could be split into multiple  manifests or modules as they grow. This would promote reuse - just as in any good programming language. Also, as well as this init.pp filw we will also  edit one other file, war.pp, to allow for easy deployment of the mrp application.

>**Note:** You can see the complete `init.pp` file <a href="https://raw.githubusercontent.com/Microsoft/PartsUnlimitedMRP/master/Labfiles/DevOps200.2x-InfrastructureasCode/Puppet/final/init.pp" target="_blank"><span style="color: #0066cc;" color="#0066cc">here on github</span></a> and you should copy and paste its content from here, or the sections below, to your local init.pp.


 > **Note:** Tomcat requires the presence of a user and group on the node to access and run the Tomcat services. The default name it uses for both is "Tomcat". There are a number of ways we could create this account and group, such as within the tomcat configuration, but we will add these accounts to the linux node via a separate class in the init.pp file. 
 
 > **Note:** We will also need to edit the permission definition for extracting .war files from our **tomcat7/webapps** folder, which we will do at the end of this task, this will allow the .war file to be automatically extracted when the tomcat service is restarted as the .pp file runs. 


Finally, if you encounter any issues see the potential issues and troubleshooting section at the end of this lab.



#### Task 5.1 Configure MongoDb ###

Let's add a class to configure mongodb. Once mongodb is configured, we want Puppet to donwload a mongo script
that contains some data for our application's database. We'll include this as part of the mongodb setup.

On the Puppet Master, edit the init.pp file of the mrpapp module:
```sh
sudo nano /etc/puppetlabs/code/environments/production/modules/mrpapp/manifests/init.pp
```

Add the following class at the bottom of the file:

```puppet
class configuremongodb {
  include wget
  class { 'mongodb': }->

  wget::fetch { 'mongorecords':
    source => 'https://raw.githubusercontent.com/Microsoft/PartsUnlimitedMRP/master/deploy/MongoRecords.js',
    destination => '/tmp/MongoRecords.js',
    timeout => 0,
  }->
  exec { 'insertrecords':
    command => 'mongo ordering /tmp/MongoRecords.js',
    path => '/usr/bin:/usr/sbin',
    unless => 'test -f /tmp/initcomplete'
  }->
  file { '/tmp/initcomplete':
    ensure => 'present',
  }
}
```

Let's examine this class:
- Line 1: We create a class (resource) called `configuremongodb`
- Line 2: We include the `wget` [module](https://forge.puppetlabs.com/maestrodev/wget) so that we can 
download files via `wget`
- Line 3: We invoke the `mondodb` resource (from the `mongodb` module we downloaded earlier). This installs
mongodb using defaults defined in the [Puppet mongodb module](https://forge.puppetlabs.com/puppetlabs/mongodb).
Believe it or not, that's all we have to do to install mondodb!
- Line 5: We invoke the `fetch` resource from the `wget` module, calling this resource `mongorecords`
- Line 6: We set the source of the file we need to download
- Line 7: We set the destination where the file must be downloaded to
- Line 10: We use the built-in Puppet resource `exec` to execute a command
- Line 11: We specify the command to execute
- Line 12: We set the path for the command invocation
- Line 13: We specify a condition using the keyword `unless`: we only want this command to execute once, so we
create a tmp file once we have inserted the records (Line 15). If this file exists, we don't execute the
command again.

>**Note**: The `->` notation on Lines 3, 9 and 14 is an "ordering arrow": it tells Puppet that it must apply the
"left" resource before invoking the "right" resource. This allows us to specify order when necessary.

Press `cntrl-O`, then `enter` to save the changes to the file without exiting.

#### Task 5.2 Configure Java ###

Add the following class below the `configuremongodb` class:

```puppet
class configurejava {
  include apt
  $packages = ['openjdk-8-jdk', 'openjdk-8-jre']

  apt::ppa { 'ppa:openjdk-r/ppa': }->
  package { $packages:
     ensure => 'installed',
  }
}
```

Let's examine this class:
- Line 2: We include the `apt` module, which will allow us to configure new Personal Package Archives (PPAs)
- Line 3: We create an array of packages that we need to install 
- Line 5: We add a PPA
- Lines 6 - 8: We tell Puppet to ensure that the package are installed. Puppet expands the array and essentially
does a for-each, installing each package in the array.

>**Note:** We can't use the Puppet `package` target to install Java since this will only install Java 7. That's
why we needed to add the PPA using the `apt` module.

Press `cntrl-O`, then `enter` to save the changes to the file without exiting.


#### Task 5.3 Create User and Group

The users and groups specified here are created on the linux system, and will be used for configuration, deployment and for starting and stopping services.

```puppet
class createuserandgroup {

group { 'tomcat':
  ensure => 'present',
  gid    => '10003',
  }

user { 'tomcat':
  ensure           => 'present',
  gid              => '10003',
  home             => '/tomcat',
  password         => '!',
  password_max_age => '99999',
  password_min_age => '0',
  uid              => '1003',
  }
}
```
Let's examine this class:
- Line 1: We call the createuserandgroup class that we defined at the start of the init.pp file. Thsi is a class we have defined ourselves that will use the resources group and user.
- Line 3-6: We use the `group` command to create the user `Tomcat` , and we use the parameter value `ensure = > present` to check if it exists and if not to craete it. We then assign a gid value to the group. we don't have to but if we were applying across multiple machines it would help with management.
- Line 8-17 : We use the `user` command to create the user `Tomcat`, we then use a range of parameter values to ensure it is created i.e. `ensure`, id values using `gid` and `uid`, we designate the password values, we leave it open and unspecified in this instance for ease of use here, but you should of course follow best practices when specifying password values and configuration.


>**Note:** The command `user` and `group` are made available to us via the `puppetlabs-accounts` module we installed earlier on the master.

#### Task 5.4 Configure Tomcat ###

Let's add a class below the `configurejava` class to configure `tomcat`:

```puppet
class configuretomcat {
  class { 'tomcat': }
 require createuserandgroup


 tomcat::instance { 'default':
  catalina_home => '/var/lib/tomcat7',
  install_from_source => false,
  package_name => 'tomcat7',

 }->

 tomcat::config::server::tomcat_users {
 'tomcat':
   catalina_base => '/var/lib/tomcat7',
   element  => 'user',
   password => 'password',
   roles => ['manager-gui','manager-jmx','manager-script','manager-status'];
 'tomcat7':
   catalina_base => '/var/lib/tomcat7',
   element  => 'user',
   password => 'password',
   roles => ['manager-gui','manager-jmx','manager-script','manager-status'];

 }->

 tomcat::config::server::connector { 'tomcat7-http':
  catalina_base => '/var/lib/tomcat7',
  port => '9080',
  protocol => 'HTTP/1.1',
  connector_ensure => 'present',
  server_config => '/etc/tomcat7/server.xml',

 }->

 tomcat::service { 'default':
  use_jsvc => false,
  use_init => true,
  service_name => 'tomcat7',

 }

}
```

Let's examine this class:
- Line 1: We create a class (resource) called `configuretomcat`
- Line 2: We invoke the `tomcat` resource (from the [tomcat module](https://forge.puppetlabs.com/puppetlabs/mongodb)
we downloaded earlier)
- Line 3: We require the `Tomcat` user account and group to be available before we can configure tomcat, so we mark the class createuserandgroup as required.
- Lines 6-9: We install Tomcat from the Tomcat module we downloaded earlier, the package name is `Tomcat7`, so we call it here, we designate the home directory where we will place our default installation and we specify that it is not fro source, this is required when installing from a downloaded package. We could install directly from a web source also if we wished. We also install the tomcat7-admin package. We will not use this here but it would provide us with the Tomcat Manager web UI.
- Lines 13-23: We create two user names for use by tomcat. These are created within the tomcat application and are used for management and configuration of tomcat. They are created within and called from the file `/var/lib/tomcat7/conf/tomcat-users.xml`. You can open and view its contents.
- Lines 26 - 31: We configure the Tomcat connector, define the protocol to use, the port number, ensuring the connector is present to allow it serve requests on these settings. We also specify the connector properties for Puppet to write to the tomcat server.xml file. Again you can open and view its contents on the puppet master at `/var/lib/tomcat7/conf/server.xml`.
- Lines 35 - 38: We configure the Tomcat service, which is our default installation. As there is only one installation of Tomcat we specify the default value that we're applying the configuration to. We ensure the service is runing and the start the service using the `use_init` value, we then specify a name for the service i.e. **tomcat7*,  connector for the Parts Unlimited MRP application. 


Press `cntrl-O`, then `enter` to save the changes to the file without exiting.
 
#### Task 5.5 Deploy a WAR File

The MRP application is compiled into a WAR file that Tomcat then uses to serve pages.

Let's specify a resource to deploy the war file for the site. Go back to the Puppet SSH session and edit the `init.pp` file. 
Add the following class at the bottom of the file:

```puppet
class deploywar {
  require configuretomcat

  tomcat::war { 'mrp.war':
    catalina_base => '/var/lib/tomcat7',
    war_source => 'https://raw.githubusercontent.com/Microsoft/PartsUnlimitedMRP/master/builds/mrp.war',
  }

 file { '/var/lib/tomcat7/webapps/':
   path => '/var/lib/tomcat7/webapps/',
   ensure => 'directory',
   recurse => 'true'
   mode => '777',
 }
}
```

Let's examine this class:
- Line 1: We create a class (resource) called `deploywar`
- Line 2: We tell Puppet to make sure that `configuretomcat` is complete before invoking this class
- Line 5: We set the `catalina base` directory so that Puppet deploys the war to our Tomcat service
- Line 6: We use the tomcat module's `war` resource to deploy our war from the `war_source`
- Line 9: we call the file class, which comes from the tomcat module and allow us ocnfigure files on our system
- Line 10: we specify the folder in question
- Line 11: we make sure it exists and is a folder before applying any configuration
- Line 12: We say to apply the configuration recursively to affect all folder beneath.
- Lilne 13: We designate a `777` permission on the folders. **Important** : This allows read, write and execute permissions to all content within this folder. We do this only in a lab environment and it should never be done in production. More restrictive permissions should be applied to specific users and their access level.

> All thats typically needed to deploy a `.war` file is to copy the `.war` file to the tomcat `/var/lib/tomcat7/webapps` folder, then once the correct permissions are in place on the `webapps` folder and the values for `unpackWARS` and `autoDeploy` in the tomcat server config file `/var/lib/tomcat7/conf/server.xml`, then also a valid user is listed and available in the `/var/lib/tomcat7/conf/tomcat-users.xml` Tomcat will extract and then deploy the .war when the service starts, hence the restart of the service in the init.pp file. When finished the lab you should open and view the content of these two `.xml` files.

#### Task 5.6 Start the Ordering Service

The MRP service calls an Ordering Service, which is a REST API managing orders in the MongoDb. This service is compiled to a 
jar file. We'll need to copy the jar file to our node and then run it in the background so that it can listen for requests.

Now we need to make sure that the ordering service is running. Again we'll add a new class at the bottom of the `init.pp` file:

```puppet
class orderingservice {
  package { 'openjdk-7-jre':
    ensure => 'installed',
  }

  file { '/opt/mrp':
    ensure => 'directory'
  }->
  wget::fetch { 'orderingsvc':
    source => 'https://raw.githubusercontent.com/Microsoft/PartsUnlimitedMRP/master/builds/ordering-service-0.1.0.jar',
    destination => '/opt/mrp/ordering-service.jar',
    cache_dir => '/var/cache/wget',
    timeout => 0,
  }->
  exec { 'stoporderingservice':
    command => "pkill -f ordering-service",
    path => '/bin:/usr/bin:/usr/sbin',
    onlyif => "pgrep -f ordering-service"
  }->
  exec { 'stoptomcat':
    command => 'service tomcat7 stop',
    path => '/usr/bin:/usr/sbin',
    onlyif => "test -f /etc/init.d/tomcat7",
  }->
  exec { 'orderservice':
    command => 'java -jar /opt/mrp/ordering-service.jar &',
    path => '/usr/bin:/usr/sbin:/usr/lib/jvm/java-8-openjdk-amd64/bin',
  }->
  exec { 'wait':
    command => 'sleep 20',
    path => '/bin',
    notify => Tomcat::Service['default']
  }
}
```

Let's examine this class:
- Line 1: We create a class (resource) called `orderingservice`
- Lines 2 - 4: We install the Java JRE required to run the application using Puppet's `package` resource
- Lines 6 - 8: We ensure that the directory `/opt/mrp` exists (Puppet creates it if it doesnt)
- Lines 9 - 14: We `wget` the ordering service binary, placing it in `/opt/mrp`
- Line 12: We specify a cache directory to ensure that the file is only downloaded once
- Lines 15 - 19: We stop the `orderingservice`, but only if it is running
- Lines 20 - 24: We stop the `tomcat7` service, but only if it is running
- Lines 25 - 28: We start the ordering service
- Lines 29 - 33: We sleep for 20 seconds to give the ordering service time to start up before `notifying`
the `tomcat` service, which triggers a refresh on the service - Puppet will re-apply the state we defined
for the service (i.e. start it if it is not running)

>**Note:** We need to wait after running the `java` command since this service needs to be running before we
start Tomcat, otherwise Tomcat grabs the port that the ordering service needs to listen on.

#### Task 5.7 Complete the mrpapp Resource ###

Go back to the top of the file and change the `mrpapp` class to look as follows to run all our resources:

```puppet
class mrpapp {
  class { 'configuremongodb': }
  class { 'configurejava': }
  class { 'createuserandgroup': }
  class { 'configuretomcat': }
  class { 'deploywar': }
  class { 'orderingservice': }
}
```

Press `cntrl-O`, then `enter` to save the changes to the file without exiting.



#### Task 5.8 Configure .war file extracton permissions ###

Our mrp application .war file is copied to the folder `/var/lib/tomcat7/webapps` as part of our init.pp file configuration. Tomcat will automatically extract .war files from the webapps folder, however to ensure this runs smoothly in our lab we will open up the permission a bit more broadly to helpo ensure fewer issues could be encountered by students. More details arond the configuration editing can eb viewed on the <a href="https://puppet.com/docs/puppet/5.5/types/file.html#file-attribute-mode" target="_blank"><span style="color: #0066cc;" color="#0066cc">Resource Type: file</span></a> page.




On the Puppet Master, oen the `war.pp` file of the tomcat module in an editor:
```sh
sudo nano /etc/puppetlabs/code/environments/production/modules/tomcat/manifests/war.pp
```

Modify the following class at the bottom of the war.pp file and modify the  value 

`mode => '0640',` to  `mode => '0777',`  i.e. from `0640` to  `0777`

It should look like the below.

```puppet
    file { "tomcat::war ${name}":
      ensure    => file,
      path      => "${_deployment_path}/${_war_name}",
      owner     => $user,
      group     => $group,
      mode      => '0777',
      subscribe => Archive["tomcat::war ${name}"],
    }
  }
}

```
Let's examine this class:
- Line 1: Is specifying the class name, of file, which comes with the tomcat module `tomcat::war`
- Line 2: is ensuring the file is present before acting on it.
- Line 3: is defining the path which in our instance is, i.e. /var/lib/tomcat7/webapps. This is where our mrp application .war file gets copied to, in the init.pp file.
- Line 4-5: This is defining the user and group owners on the files.
- Line 6: This is defining the file permissions, 777 allows for read, write and execute for everyone. **You should not do this in a production environment** as you will expose your environment and risk allowing potentially malicious code be executed.
-

### Task 6: Run the Puppet Configuration on the Node

1. Run the Puppet Agent
    On the partsmrp SSH session, again force Puppet to update the node's configuration:

    ```sh
    sudo puppet agent --test
    ```

    This first run will take a few moments - there is lots to download and install for the first run! Next time the Puppet agent runs,
    it will verify that the existing environment is correctly configured - that should be much quicker since the services will already
    be installed and configured.

1. Verify that Tomcat is running

    Open a browser and browse to port `9080` of the partsmrp machine. You can get the name of the machine by clicking on the Public IP
    resource for the machine in Azure (just like you did to get the url of the puppet master earlier). Once you open the browser, you 
    should see the following Tomcat confirmation page:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\20.jpg)

1. Verify that the PartsUnlimitedMRP Application is running correctly

    Now you can ensure that the configuration is correct by opening a browser to the Parts Unlimited MRP application. The address
    will be http://partsmrp-public-ip:9080/mrp where _partsmrp-public-ip_ is the public ip or DNS name of the
    partsmrp VM (you can get it by clicking on the VM in the resource group in the Azure Portal).

    ![](..\assets\pumrpdeploywithpuppet-jan2018\18.jpg)

    If you click on the Orders button, you should see the orders page:

    ![](..\assets\pumrpdeploywithpuppet-jan2018\19.jpg)

>**Note:** You can see the complete `init.pp` file <a href="https://raw.githubusercontent.com/Microsoft/PartsUnlimitedMRP/master/Labfiles/DevOps200.2x-InfrastructureasCode/Puppet/final/init.pp" target="_blank"><span style="color: #0066cc;" color="#0066cc">here on github</span></a>.


##### Potential issues and troubleshooting #####

- If you encounter the below issue on first run of the configuration, this is related to a reference to a java package in the mongodb installation and is only potentialyl encountered on the first run on the configuration. If you re-run the configuration by running the command  `sudo puppet agent --test --debug` on the node, it should through to the end wihtout error.

    ```puppet
    Error: Execution of '/usr/bin/apt-get -q -y -o DPkg::Options::=--force-confold install openjdk-7-jre' returned 100: Reading package lists...
    Building dependency tree...
    Reading state information...
    Package openjdk-7-jre is not available, but is referred to by another package.
    This may mean that the package is missing, has been obsoleted, or
    is only available from another source

    ```

<h3><span style="color: #0000CD;"> Summary</span></h3> 
In this lab, you learned how to create the Puppet infrastructure and deploy the Parts Unlimited MRP app to the nodes while managing configuration drift.